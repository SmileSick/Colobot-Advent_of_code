extern void object::AoC_2015_3()
{
	ipf(10000);
	int p1 = checking(1);
	int p2 = checking(2);
	message("Part 1: " + p1 + ", Part 2: " + p2);
}

int checking(int num)
{
	int count = 1;
	file handle = new file();
	handle.open("input.txt", "r");
	string text = handle.readln();
	handle.close();
	point men[];
	for(int i = 0; i < num; i++)
	{
		men[i] = new point(0, 0, 0);
	}
	string array = "x0y0;";
	int counttext = strlen(text);
	string temp;
	for(int i = 0; i < counttext; i++)
	{
		temp = strmid(text, i, 1);
		if(temp == "<") men[i%num].x--;
		else if(temp == ">") men[i%num].x++;
		else if(temp == "^") men[i%num].y++;
		else men[i%num].y--;
		temp = "x" + men[i%num].x + "y" + men[i%num].y + ";";
		if(strfind(array, temp) == nan)
		{
			array += temp;
			count++;
		}
	}
	return count;
}
